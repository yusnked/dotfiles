if [[ $DOTS_OS == Darwin && -r /etc/zshrc ]]; then
    source '/etc/zshrc'
fi

typeset -xU fpath FPATH
fpath=(
    "$ZDOTDIR/functions"(N-/)
    "$NIX_DATA_DIR/zsh/site-functions"(N-/)
    '/nix/var/nix/profiles/default/share/zsh/site-functions'(N-/)
    $fpath
)

source "$XDG_CONFIG_HOME/_b-shell/env_interactive.sh"

source "$ZDOTDIR/rc/directories.zsh"
source "$ZDOTDIR/rc/options.zsh"
source "$ZDOTDIR/rc/prompt.zsh"

# zsh-defer dummy
autoload -Uz _zsh-defer-dummy
zsh-defer() { _zsh-defer-dummy "$@"; }

# sheldon
if type sheldon &>/dev/null; then
    () {
        export SHELDON_CONFIG_DIR="$XDG_CONFIG_HOME/sheldon"
        export SHELDON_CONFIG_FILE="$SHELDON_CONFIG_DIR/zsh-plugins.toml"
        local cache_dir="$XDG_CACHE_HOME/sheldon"
        local cache_file="$cache_dir/zsh-plugins.zsh"
        if [[ ! -r $cache_file || $SHELDON_CONFIG_FILE -nt $cache_file ]]; then
            mkdir -p $cache_dir
            sheldon source > $cache_file
        fi
        source "$cache_file"
    }
fi

# ===== Delayed loading from here down =====

# Flatpak
if [[ $DOTS_OS == Linux ]]; then
    source "$XDG_CONFIG_HOME/flatpak/generate_flatpak_app_func.sh"
fi

zsh-defer -p source "$ZDOTDIR/rc/alias.zsh"
zsh-defer -p source "$ZDOTDIR/rc/bindkey.zsh"
zsh-defer -p source "$ZDOTDIR/rc/completion.zsh"
zsh-defer -p source "$ZDOTDIR/rc/functions.zsh"

# fzf
zsh-defer -p source "$XDG_CONFIG_HOME/fzf/fzf.sh"

# zoxide
zsh-defer -p source "$XDG_CONFIG_HOME/zoxide/zoxide.sh"

zsh-defer -p ensure_zcompiled "$ZDOTDIR/.zshrc"
zsh-defer -p unset -f ensure_zcompiled source

# zprof | less
