# shellcheck disable=all

if [[ $DOTS_OS == Darwin && -r /etc/zshrc ]]; then
    source '/etc/zshrc'
fi

typeset -xU FPATH fpath
fpath=(
    "$ZDOTDIR/functions"(N-/)
    "$HOME/.nix-profile/share/zsh/site-functions"(N-/)
    '/nix/var/nix/profiles/default/share/zsh/site-functions'(N-/)
    '/opt/homebrew/share/zsh/site-functions'(N-/)
    '/usr/share/zsh/vendor-completions'(N-/)
    $fpath
)

source "$DOTS_SHELLS_DIR/env_interactive.sh"

source "$ZDOTDIR/rc/directories.zsh"
source "$ZDOTDIR/rc/options.zsh"
source "$ZDOTDIR/rc/prompt.zsh"

# zsh-defer dummy
autoload -Uz _zsh-defer-dummy
zsh-defer() { _zsh-defer-dummy "$@"; }

# sheldon
source "$DOTS_SHELLS_DIR/plugins.sh"

# ===== Delayed loading from here down =====

zsh-defer -p source "$ZDOTDIR/rc/alias.zsh"
zsh-defer -p source "$ZDOTDIR/rc/bindkey.zsh"
zsh-defer -p source "$ZDOTDIR/rc/completion.zsh"
zsh-defer -p source "$ZDOTDIR/rc/functions.zsh"

# zoxide
zsh-defer -p -c 'type zoxide &>/dev/null && eval "$(zoxide init zsh --hook pwd)"'

# LS_COLORS setting by vivid
zsh-defer -p -c '() {
    if type vivid &>/dev/null; then
        [[ $COLORTERM != truecolor ]] && local no_tc=1
        export LS_COLORS=$(eval vivid ${no_tc+--color-mode 8-bit} generate dracula)
    fi
}'

# mise
zsh-defer -p -c 'type mise &>/dev/null && eval "$(mise activate zsh)"'

zsh-defer -p ensure_zcompiled "$ZDOTDIR/.zshrc"
zsh-defer -p unset -f ensure_zcompiled source

# zprof | less
