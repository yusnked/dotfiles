# zmodload zsh/zprof && zprof >/dev/null

function ensure_zcompiled {
    local compiled="$1.zwc"
    if [[ ! -r "$compiled" || "$1" -nt "$compiled" ]]; then
        local pattern1="^${XDG_CONFIG_HOME//./\\.}/.*$"
        local pattern2="^${${XDG_DATA_HOME:-$HOME/.local/share}//./\\.}/sheldon/.*$"
        if [[ $1 =~ $pattern1 || $1 =~ $pattern2 ]]; then
            zcompile $1
        fi
    fi
}

function source {
    ensure_zcompiled $1
    builtin source $1
}

typeset -xU path PATH

export DOTS_OS="${DOTS_OS:-$(uname -s)}"
if [[ $DOTS_OS == Darwin ]]; then
    unsetopt GLOBAL_RCS
    if [[ -x /usr/libexec/path_helper ]]; then
        eval $(/usr/libexec/path_helper -s)
    fi
fi

source "$HOME/.config/_b-shell/env.sh"

ensure_zcompiled $ZDOTDIR/.zshenv
