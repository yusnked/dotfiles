# zmodload zsh/zprof && zprof >/dev/null

function ensure_zcompiled {
    local compiled="$1.zwc"
    if [[ ! $1 -ot "$compiled" ]]; then
        local pattern patterns=(
            "$XDG_CONFIG_HOME/"
            "${XDG_DATA_HOME:-$HOME/.local/share}/sheldon/"
            "${XDG_DATA_HOME:-$HOME/.local/share}/_shells/"
        )
        for pattern in "${patterns[@]}"; do
            if [[ $1 != "${1#"$pattern"}" ]]; then
                zcompile "$1"
                break
            fi
        done
    fi
}

function source {
    ensure_zcompiled "$1"
    builtin source "$1"
}

typeset -xU PATH path

export DOTS_OS="${DOTS_OS:-$(uname -s)}"
if [[ $DOTS_OS == Darwin ]]; then
    unsetopt GLOBAL_RCS
    if [[ -x /usr/libexec/path_helper ]]; then
        eval "$(/usr/libexec/path_helper -s)"
    fi
fi

source "${DOTS_SHELLS_DIR:-$HOME/.config/_shells}/env.sh"

ensure_zcompiled "$ZDOTDIR/.zshenv"
